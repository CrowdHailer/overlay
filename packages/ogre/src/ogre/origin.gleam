import gleam/http
import gleam/http/request
import gleam/int
import gleam/option.{type Option, None, Some}
import gleam/result
import gleam/uri

pub type Origin {
  Origin(scheme: http.Scheme, host: String, port: Option(Int))
}

pub fn https(host: String) -> Origin {
  Origin(http.Https, host, None)
}

pub fn http(host: String) -> Origin {
  Origin(http.Http, host, None)
}

pub fn to_request(origin: Origin) -> request.Request(String) {
  let Origin(scheme:, host:, port:) = origin
  let request =
    request.new()
    |> request.set_scheme(scheme)
    |> request.set_host(host)
  case port {
    Some(port) -> request.set_port(request, port)
    None -> request
  }
}

pub fn to_uri(origin: Origin) -> uri.Uri {
  let Origin(scheme, host, port) = origin
  uri.Uri(
    scheme: Some(http.scheme_to_string(scheme)),
    userinfo: None,
    host: Some(host),
    port: port,
    path: "",
    query: None,
    fragment: None,
  )
}

pub fn from_uri(uri: uri.Uri) -> Result(Origin, Nil) {
  case uri {
    uri.Uri(scheme: Some(scheme), host: Some(host), port: port, ..) ->
      case http.scheme_from_string(scheme) {
        Ok(scheme) -> Ok(Origin(scheme:, host:, port:))
        Error(Nil) -> Error(Nil)
      }
    _ -> Error(Nil)
  }
}

pub fn to_string(origin: Origin) -> String {
  let Origin(scheme, host, port) = origin
  let scheme = http.scheme_to_string(scheme)
  let port = case port {
    None -> ""
    Some(port) -> ":" <> int.to_string(port)
  }
  scheme <> "://" <> host <> port
}

pub fn from_string(uri: String) -> Result(Origin, Nil) {
  use uri <- result.try(uri.parse(uri))
  from_uri(uri)
}
